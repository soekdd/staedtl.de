SELECT ?article ?countryLabel ?cityLabel ?location (GROUP_CONCAT(DISTINCT ?stateLabel; SEPARATOR=", ") AS ?stateLabels) (SAMPLE(?locationMapURL) as ?locationMap) (SAMPLE(?imageURL) as ?image) (SAMPLE(?flagURL) as ?flag) (SAMPLE(?population) as ?inhabitants) (SAMPLE(?altitude) as ?height) (MIN(?plz) as ?plzMin) (MAX(?plz) as ?plzMax) WHERE {  
  ?city (wdt:P31/(wdt:P279*)) wd:Q515.
  ?country wdt:P30 wd:Q46.
  #?city wdt:P17 wd:Q142.
        #Q142. # wd:Q183).  
  ?city wdt:P1082 ?population.
  ?city wdt:P625 ?location.
  OPTIONAL {?city wdt:P2044 ?altitude.}
  ?city wdt:P17 ?country.
  OPTIONAL {?city wdt:P281 ?plz.}
  OPTIONAL {?city wdt:P94 ?flagURL.}
  OPTIONAL {?city wdt:P18 ?imageURL.}
  OPTIONAL {?city wdt:P242 ?locationMapURL.}
  OPTIONAL {?city wdt:P131 ?state.}
  OPTIONAL {
    ?article schema:about ?city .
    ?article schema:inLanguage "de" .
    ?article schema:isPartOf <https://de.wikipedia.org/> .
  }    
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],de".
                                           ?state rdfs:label ?stateLabel.
                                           ?city rdfs:label ?cityLabel.
                                           ?country rdfs:label ?countryLabel.}  
  FILTER (?population>250000)
  FILTER (?country != wd:Q159)
  FILTER (?country != wd:Q1747689)
  FILTER (?country != wd:Q12560)
  FILTER (?country != wd:Q175276)
  FILTER (?country != wd:Q199442)
}
GROUP BY ?article ?countryLabel ?cityLabel ?location